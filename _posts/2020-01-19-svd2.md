---
title: "[Machine Learning with Julia] Singular Vector Decomposition Matrix Completion"
date: 2020-01-19
tags: [machine learning, data science, neural network, svd]

excerpt: "Machine Learning, Singular Vector Decomposition, Data Science"
mathjax: "true"
---

# Learning to complete a matrix with missing entries

Let's describe an algorithm that employs the SVD to take advantage of the redundancy in a low-rank matrix. This will allow us to fill in the missing entries without assuming any particular pattern.

Let's consider $$\tilde{A}$$ as a matrix contains missing entries.

The first step is to form the matrix $$\hat{A}_0$$ from $$\tilde{A}$$ as
- $$\hat{A}_0[i,j] = \tilde{A}[i,j]$$ if $$\tilde{A}[i,j]$$ is not missing, and
- $$\hat{A}_0[i,j] =0 $$ if $$\tilde{A}[i,j]$$ is a missing entry.

Then we perform a $$rank-K$$  truncated approximation of  $$A_0$$  to form  $$A_1$$ , which is given by
$$\hat{A}_1 = \sum_{i=1}^{k}  \sigma^{(0)}_i u^{(0)}_i (v^{(0)}_i)^T,$$
where $$\{\sigma_i^{(0)},u^{(0)}_i,v_{i}^{(0)}\}$$ are the singular value triplet associated with the SVD of  $$A_0$$.

Next, we compute the matrix $$\tilde{A}_2$$ where

- \tilde{A}_2[i,j] = A[i,j] \textrm{ for } i, j \notin \textrm{missing}
- \tilde{A}_2[i,j] = \hat{A}_1[i,j] \textrm{ for } i, j \in \textrm{missing}.

We repeat this process until convergence.

Implement this algorithm via the function **completeAsvd**.

```julia
using LinearAlgebra, Arpack

"""
Inputs:
* `A`: matrix with missing entries
* `k`: rank of matrix
* `iters`: Number of iterations to run
* `error_tol`: error tolerance

Outputs:
* `A_hat`: Completed Matrix
* `err`: vector of relative Frobenius norm errors at each iteration
"""
function completeAsvd(A::Matrix, k::Number; iters::Integer=1000, error_tol::Number=1e-9)
    missing_idx = ismissing.(A) # Location of missing entries
    nonmissing_idx = .!missing_idx

    error = Inf
    err = Float64[]

    idx = 1 # Iteration index

    A_hat = replace(A, missing=>0) # Estimate

    while (error > error_tol && idx <= iters)

        A_hat[nonmissing_idx] = A[nonmissing_idx] # Force Known Entries: Projection Step

        UsV = svds(A_hat; nsv=k)[1]
        U, s, V = UsV.U, UsV.S, UsV.V

        A_hat = U * Diagonal(s) * V' # Rank k fit

        error = sqrt(sum(abs2, skipmissing(A - A_hat)) / sum(abs2, skipmissing(A))) # Normalized error on known entries
        push!(err,error)

        idx += 1

    end
    return A_hat, err
end
```

A plot of the error shows the nature of the learning process.

```julia
scatter(error; yscale=:log10, xlabel="Iterations", ylabel="Error")
```

<img src="{{ site.url }}{{ site.baseurl }}/images/ml1/mat_comp_rate.png" alt="">

## 1. Application : Low rank image reconstruction

We now apply this method to reconstruct a matrix (obtained from an image) that is low rank. We first load the matrix as in the next cell.

```julia
image = load("mit_logo.png")
image_matrix_gray = float(Gray.(image));
heatmap(image_matrix_gray, color=:grays, yflip=:true, ticks=[], clim=(0, 1))
```
<img src="{{ site.url }}{{ site.baseurl }}/images/ml1/logo1.png" alt="">

`Matrix with missing values`
```julia
p = 0.7
image_missing = image_matrix_gray |> allowmissing
rand_idx = rand(size(image_matrix_gray)...) .>= p
image_missing[rand_idx] .= missing
image_missing_zeros = replace(image_missing, missing=>0.0)
heatmap(image_missing_zeros; color=:grays, yflip=:true, ticks=[], clim=(0, 1))
```
<img src="{{ site.url }}{{ site.baseurl }}/images/ml1/logo2.png" alt="">

`Estimation after 1 iteration`
```julia
k = 4 ## note that the numerical rank of the MIT logo is 10 with 4 much-more significant singular values
image_hat, error = completeAsvd(image_missing, k; iters=1)
heatmap(image_hat; color=:grays, yflip=:true, ticks=[], title="Error = $(round(error[end]; digits=5))")
```
<img src="{{ site.url }}{{ site.baseurl }}/images/ml1/logo3.png" alt="">

`Estimation after 50 iterations`
```julia
image_hat, error = completeAsvd(image_missing, k; iters=50)
heatmap(image_hat; color=:grays, yflip=:true, ticks=[], title="Error = $(round(error[end]; digits=5))")
```

<img src="{{ site.url }}{{ site.baseurl }}/images/ml1/logo4.png" alt="">
