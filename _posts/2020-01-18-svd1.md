---
title: "Machine Learning with Julia: Singular Vector Decomposition Background Subtraction"
date: 2020-01-18
tags: [machine learning, data science, neural network, svd]

excerpt: "Machine Learning, Singular Vector Decomposition, Data Science"
mathjax: "true"
---

# 1. Background subtraction using the Singular Vector Decomposition

SVD decomposes a matrix into a sum of rank-one outer product matrices. This powerful representation enables a seemingly magical feat when applied to stable video footage.

We will illustrate with a surveillance video, currently stored as a .jld file. The command JLD.load produces a Dictionary of the variables in the file.

```julia
load("lobby.jld")
```
We can access a specific variable, movie_cube in this case, as follows:

```julia
file = load("lobby.jld")
varname = "movie_cube"

movie_cube = file[varname]

# data are compressed using 16-bit containers;
# we prefer 64-bit for computation
movie_cube = Float64.(movie_cube)
```

The movie_cube datacube is composed of 650 frames of a video, where each frame is a 128-by-160 image. We will scale the elements of the datacube so that they lie between 0 and 1, then display the first frame of this video.

```julia
function rescale_zero_one(A::Array)
    B = float(A)
    B .-= minimum(B)
    B /= maximum(B)
    return B
end

movie_cube = rescale_zero_one(movie_cube)
```

We can explore different frames of the video using following code.

```julia
include("./webplayer.jl")
playvideo(
    [movie_cube], ["Original movie"];
    frames_per_second=120)
```

<img src="{{ site.url }}{{ site.baseurl }}/images/ml1/subtraction.png" alt="">

## 1.1. Decomposing the movie matrix with the svd

We will now use the SVD to decompose the movie. The movie is stored as an  ùëö√óùëõ√óùë°  3-D array, where

- $$m$$ is the number of rows of pixels in each frame,
- $$n$$ is the number of columns of pixels in each frame, and
- $$t$$ is the number of frames in the movie

To decompose using the SVD, we first reshape the movie array into an $$mn \times t$$ matrix $$A$$. Each column of the matrix corresponds to a frame (and contains $$mn$$ pixel values).

We then compute the rank-$$k$$  truncated singular value decompositon of $$A$$. In other words, if $$A$$ has rank $$r$$ and has SVD

$$A = \sum_{i=1}^{r} \sigma_i u_i v_i^T,$$

then  $$A_k$$ , the $$rank-k \leq r$$ truncted SVD of $$A$$, is

$$A_k = \sum_{i=1}^{k} \sigma_i u_i v_i^T,$$

and $$(A‚àíA_k)$$ is the residual matrix. We will compute the residual $$(A‚àíA_k)$$ directly, but it is good to observe that it has SVD

$$A - A_k = \sum_{i=1}^{r} \sigma_i u_i v_i^T - \sum_{i=1}^{k } \sigma_i u_i v_i^T = \sum_{i=k+1}^{r} \sigma_i u_i v_i^T.$$

The following code is about a function **svdize_cube** that takes as input the $$m\times n \times t$$ movie, and a rank $$k$$. The function should

1. reshape the movie into an $$mn \times t$$ matrix
2. compute the truncated SVD and residual
3. reshape both truncated SVD and residual into $$m \times n \times t$$ arrays
4. return the reshaped truncated SVD, reshaped residuals, and singular values

```julia
function svdize_cube(cube, k)
    # Reshape 3-D array into matrix where each column is a frame
    m, n, l = size(cube) # cube is an m x n x l 3-D array
    matrix = reshape(cube, m*n, l) # Convert to mn x l matrix

    # Compute rank k truncated SVD and residual
    UsV = svds(matrix; nsv=k)[1]
    Uk = UsV.U
    sk = UsV.S
    Vk = UsV.Vt

    matrix_k = Uk * Diagonal(sk) * Vk
    matrix_residual = matrix - matrix_k

    # Reshape back into 3-D m x n x numFrames arrays
    cube_k = reshape(matrix_k, m, n, l)
    cube_residual = reshape(matrix_residual, m, n, l)

    return cube_k, cube_residual, sk
end
```
First we compute $$A_1$$ using the function we just defined.

```julia
k = 1
# Compute truncated SVD-version of movie
movie_cube_k, movie_cube_residual, _ = svdize_cube(movie_cube, k)

movie_cube_k = rescale_zero_one(movie_cube_k)
movie_cube_residual = rescale_zero_one(movie_cube_residual);
```

Now let's examine the frames of the $$rank-K$$ approximation!

```julia
playvideo(
    [movie_cube, movie_cube_k],
    ["Original movie","Rank-$k approximation"],
    frames_per_second=120
)
```
<img src="{{ site.url }}{{ site.baseurl }}/images/ml1/subtraction2.png" alt="">

Let us examine the residual - after all, movie_cube = movie_cube_k + movie_cube_residual

```julia
playvideo(
    [movie_cube_k, movie_cube_residual, movie_cube],
    ["Rank-$k approximation", "Residual", "Original Movie"],
    frames_per_second=120
)
```
<img src="{{ site.url }}{{ site.baseurl }}/images/ml1/subtraction3.png" alt="">

### 1.2. What information do the right singular vectors represent?

Recall that the SVD returns the left and right singular vectors. So we may deduce the structure of the low-rank matrix hiding inside this movie. Let us first try to understand what the right singular vectors of the decomposed movie convey.

We begin by examining first right singular vector, $$V[:,1]$$, more closely.

```julia
joinframes(frame::Integer, vids::Tuple) = hcat((vid[:, :, frame] for vid in vids)...)

@manipulate for frame = (100, 194, 397, 450)
    # Plot first right singular vector
    p1 = plot(
        V,
        linestyle=:solid,
        title="First right singular vector"
    )

    # Denote current frame
    plot!(
        (frame, V[frame, 1]),
        marker=:circle, # current point
        color=:black,
        label="Current frame"
    )

    # Plot movie frames
    p2 = plotframe(
        joinframes(
            frame,
            (movie_cube,
            movie_cube_k,
            movie_cube_residual)
        ),
        title="Movie | Rank-k approx. | Residual",
        clim=(0, 1)
    )
    vbox(plot(p1, p2; layout=Plots.grid(2, 1, heights=[0.5, 0.5])))

end
```
<img src="{{ site.url }}{{ site.baseurl }}/images/ml1/v1.png" alt="">
---
<img src="{{ site.url }}{{ site.baseurl }}/images/ml1/v1_2.png" alt="">

The right singular vector is encoding the "on-off-on" behavior of the front row of lights! Note that all elements of  $$V[:,1]$$  are positive, so the "off" portion corresponds to 0 in the appropriate segment of the vector.

Let us now compare the low-rank decompositions obtained by setting  $$K=1$$  versus  $$K=2$$ , as in the next cell. When  $$K=2$$ , we will also plot V[:, 2] and try to understand what it captures.

In the same way as $$K=1$$, we can subtract background from the video by setting $$K=2$$.

<img src="{{ site.url }}{{ site.baseurl }}/images/ml1/v2.png" alt="">

Therefore we can find that setting  $$K=2$$  does indeed better capture the background than  $$K=1$$.

## 1.3. Summary

>We saw how the SVD allows us to capture low-rank matrices. The ability to spot low rank matrices is a critical skill in computational data science. Rearrangement of the data can induce low rank structure, which the SVD can then expose.
